plugins {
    id "fabric-loom" version "0.4-SNAPSHOT" apply false
    id "maven-publish"
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "fabric-loom"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = "com.sollyw.quoll"

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    }

    repositories {
        maven {
            url 'https://jitpack.io'
        }
        mavenLocal()
    }

    processResources {
        inputs.property "version", project.version

        from(sourceSets.main.resources.srcDirs) {
            include "fabric.mod.json"
            expand "version": project.version
        }

        from(sourceSets.main.resources.srcDirs) {
            exclude "fabric.mod.json"
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = "sources"
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact(remapJar) {
                    builtBy remapJar
                }
                artifact(sourcesJar) {
                    builtBy remapSourcesJar
                }
            }
        }

        repositories {
            mavenLocal()
        }
    }
}
